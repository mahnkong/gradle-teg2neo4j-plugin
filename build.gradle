buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.4"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.gradle.plugin-publish"

group = 'com.github.mahnkong'
version = '1.1.4'

repositories {
    jcenter()
    maven { url "http://m2.neo4j.org/content/groups/public" }
}

ext {
    neo4jVersion = '2.3.2'
    scmUrl = 'https://github.com/mahnkong/gradle-teg2neo4j-plugin'
    projectDescription = 'A Gradle-Plugin which enables the "Gradle Task Execution Graph" of a project\'s build to be stored inside a Neo4j instance'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7


// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.neo4j:neo4j-jdbc:${neo4jVersion}"

    testCompile "org.neo4j.test:neo4j-harness:${neo4jVersion}"
    testCompile 'junit:junit:4.12'
    testCompile gradleTestKit()

    testRuntime files(createClasspathManifest)
}

jar {
    from "LICENSE"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        artifacts(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().appendNode('name', project.name)
                asNode().appendNode('description', project.ext.projectDescription)
                asNode().appendNode('url', project.ext.scmUrl)
                asNode().appendNode('scm').
                        appendNode('url', project.ext.scmUrl).parent().
                        appendNode('connection', "scm:git:${project.ext.scmUrl}.git")
                asNode().appendNode('licenses').appendNode('license').
                        appendNode('name', 'Apache License, Version 2.0').parent().
                        appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.html')
                asNode().appendNode('developers').appendNode('developer').
                        appendNode('id', 'mahnkong')
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

pluginBundle {
    website = project.ext.scmUrl
    vcsUrl = project.ext.scmUrl
    description = project.ext.projectDescription
    tags = ['neo4j', 'task', 'graph']

    plugins {
        gteg2neo4jPlugin {
            id = "${group}.gteg2neo4j"
            displayName = 'Gradle TaskExecutionGraph to Neo4j plugin'
        }
    }
}

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

	publications = ['artifacts']
    pkg {
        repo = "maven"
        // it is the name that appears in bintray when logged
        name = project.name
        websiteUrl = project.ext.scmUrl
        vcsUrl = project.ext.scmUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = System.getenv("BINTRAY_GPG_PASSWORD") //Optional. The passphrase for GPG signing'
            }
            mavenCentralSync {
                sync = true //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = System.getenv("BINTRAY_USER") //OSS user token
                password = System.getenv("BINTRAY_OSS_PASSWORD") //OSS user password
            }
        }
    } 
}

bintrayUpload.dependsOn('test')
publishPlugins.dependsOn('test')
