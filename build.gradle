buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.4"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

group = 'com.github.mahnkong'
version = '1.1.3'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.gradle.plugin-publish"

repositories {
    jcenter()
    maven { url "http://m2.neo4j.org/content/groups/public" }
}

ext {
    neo4jVersion = '2.3.2'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7


// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.neo4j:neo4j-jdbc:${neo4jVersion}"

    testCompile "org.neo4j.test:neo4j-harness:${neo4jVersion}"
    testCompile 'junit:junit:4.12'
    testCompile gradleTestKit()

    testRuntime files(createClasspathManifest)
}

jar {
    from "LICENSE"
}

publishing {
    publications {
        artifacts(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

pluginBundle {
    website = 'https://github.com/mahnkong/gradle-teg2neo4j-plugin'
    vcsUrl = 'https://github.com/mahnkong/gradle-teg2neo4j-plugin'
    description = 'A Gradle-Plugin which enables the "Gradle Task Execution Graph" of a project\'s build to be stored inside a Neo4j instance'
    tags = ['neo4j', 'task', 'graph']

    plugins {
        gteg2neo4jPlugin {
            id = 'com.github.mahnkong.gteg2neo4j'
            displayName = 'Gradle TaskExecutionGraph to Neo4j plugin'
        }
    }
}

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

	publications = ['artifacts']
    pkg {
        repo = "maven"
        // it is the name that appears in bintray when logged
        name = "gradle-teg2neo4j-plugin"
        websiteUrl = 'https://github.com/mahnkong/gradle-teg2neo4j-plugin'
        vcsUrl = 'https://github.com/mahnkong/gradle-teg2neo4j-plugin'
        licenses = ["Apache-2.0"]
        publish = true
        version {
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = System.getenv("BINTRAY_GPG_PASSWORD") //Optional. The passphrase for GPG signing'
            }
            mavenCentralSync {
                sync = true //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = System.getenv("BINTRAY_USER") //OSS user token
                password = System.getenv("BINTRAY_OSS_PASSWORD") //OSS user password
            }
        }
    } 
}
