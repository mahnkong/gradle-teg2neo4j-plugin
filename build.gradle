apply plugin: 'groovy'
apply plugin: 'maven'

group = 'mahnkong'
version = '1.0.0-SNAPSHOT'

ext {
    neo4jVersion = '2.3.2'
}

repositories {
    jcenter()
    maven {
        url "http://m2.neo4j.org/content/groups/public"
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.neo4j:neo4j-jdbc:${neo4jVersion}:jar-with-dependencies"
    testCompile 'junit:junit:4.12'
    testCompile gradleTestKit()
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('../repo'))
        }
    }
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}