repositories {
    jcenter()
    maven {
        url "http://m2.neo4j.org/content/groups/public"
    }
}

group = 'mahnkong'
version = '1.0.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'maven'

ext {
    neo4jVersion = '2.3.2'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

configurations {
    uberjarLib
    compile.extendsFrom(uberjarLib)
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    uberjarLib "org.neo4j:neo4j-jdbc:${neo4jVersion}:jar-with-dependencies"

    testCompile "org.neo4j.test:neo4j-harness:${neo4jVersion}"
    testCompile 'junit:junit:4.12'
    testCompile gradleTestKit()

    testRuntime files(createClasspathManifest)
}

task uberjar(type: Jar) {
    baseName = project.name + '-with-dependencies'
    from {
        configurations.uberjarLib.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

uploadArchives() {
    repositories {
        mavenLocal()
    }
}

uploadArchives.dependsOn('test')
